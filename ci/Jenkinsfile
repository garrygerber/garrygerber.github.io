pipeline {
    agent any
    
    environment {
        GITHUB_REPO = 'garrygerber/garrygerber.github.io'
        NAMESPACE = 'garry'
        GITHUB_CRED_ID = 'GITHUB_CRED_ID'
        AKS_CLUSTER_NAME = 'devops-interview-aks'
        RESOURCE_GROUP = 'devops-interview-rg'
        HELM_REPO_NAME = 'simple-web'
        HELM_REPO_URL = 'https://garrygerber.github.io'
        KUBECONFIG = './kubeconfig'
    }
    
    parameters {
        choice(name: 'ACTION', choices: ['Deploy', 'Destroy'], description: 'Select whether to deploy or destroy a Helm release')
    }
    
    stages {
        stage('Setup') {
            steps {
                script {
                    sh """
                        az login -i
                        az aks get-credentials --resource-group ${RESOURCE_GROUP} --name ${AKS_CLUSTER_NAME} --file ${KUBECONFIG}
                        kubelogin convert-kubeconfig -l msi
                    """
                    
                    // Check if the Helm repo exists, add if it doesn't
                    def repoExists = sh(script: "helm repo list | grep -q ${HELM_REPO_NAME}", returnStatus: true)
                    if (repoExists != 0) {
                        echo "Helm Repo is Missing, Adding repository ${HELM_REPO_NAME}"
                        sh "helm repo add ${HELM_REPO_NAME} ${HELM_REPO_URL}"
                    }
                    // Update Helm repos
                    sh "helm repo update"
                }
            }
        }

        stage('Select Chart') {
            steps {
                script {
                    def chartNames = sh(script: """
                        helm search repo ${HELM_REPO_NAME}/ | grep ${HELM_REPO_NAME}/ | awk '{print \$1}' | sed 's|${HELM_REPO_NAME}/||g' | sort -u
                    """, returnStdout: true).trim()
                    
                    if (chartNames) {
                        def chartList = chartNames.split('\n').toList()
                        def selection = input(
                            message: "Select the chart to ${params.ACTION}:",
                            parameters: [choice(name: 'CHART', choices: chartList, description: 'Available charts')]
                        )
                        env.SELECTED_CHART = selection
                    } else {
                        error "No charts found in the charts directory."
                    }
                }
            }
        }
        
        stage('Select Release') {
            when {
                expression { params.ACTION == 'Deploy' }
            }
            steps {
                script {
                    def versions = sh(script: """
                        helm search repo ${HELM_REPO_NAME}/${env.SELECTED_CHART} --versions | grep ${HELM_REPO_NAME}/${env.SELECTED_CHART} | awk '{print \$2}' | sort -rV
                    """, returnStdout: true).trim()
                    
                    if (versions) {
                        def versionList = versions.split('\n').toList()
                        def selection = input(
                            message: 'Select the version to deploy:',
                            parameters: [choice(name: 'VERSION', choices: versionList, description: 'Available versions')]
                        )
                        env.RELEASE_TAG = selection
                    } else {
                        error "No versions found for ${env.SELECTED_CHART}."
                    }
                }
            }
        }

        stage('Custom Values') {
            when {
                expression { params.ACTION == 'Deploy' }
            }
            steps {
                script {
                    def customValues = input(
                        message: 'Enter custom values for the Helm chart (optional)',
                        parameters: [
                            string(name: 'CUSTOM_VALUES', 
                                    description: '''Enter custom values in key=value format, separated by commas. Keys can be quoted if they contain special characters. Leave empty for default values.\nExample:\nreplicaCount=2, image.tag=v1.0.0, ingress.enabled=true, resources.limits.cpu=100m, resources.limits.memory=128Mi, "my.special.key"="special value"''', 
                                    defaultValue: '')
                        ]
                    )
                    
                    if (customValues.trim()) {
                        env.CUSTOM_VALUES = customValues.split(',').collect { "--set ${it.trim()}" }.join(' ')
                    } else {
                        env.CUSTOM_VALUES = ''
                    }
                }
            }
        }

        stage('Deploy Helm Chart') {
            when {
                expression { params.ACTION == 'Deploy' }
            }
            steps {
                script {
                    def deployCommand = """
                        helm upgrade --install ${env.SELECTED_CHART} ${HELM_REPO_NAME}/${env.SELECTED_CHART} \
                        --namespace ${NAMESPACE} \
                        --version ${env.RELEASE_TAG} \
                        --wait \
                        --timeout 5m \
                        --kubeconfig ${KUBECONFIG} \
                        ${env.CUSTOM_VALUES}
                    """
                    
                    sh deployCommand
                    
                    timeout(time: 2, unit: 'MINUTES') {
                        sh "kubectl wait --for=condition=Ready pods -l app.kubernetes.io/instance=${env.SELECTED_CHART} -n ${NAMESPACE} --timeout=120s"
                    }
                }
            }
        }
        
        stage('Destroy Helm Release') {
            when {
                expression { params.ACTION == 'Destroy' }
            }
            steps {
                script {
                    def existingReleases = sh(script: """
                        helm list -n ${NAMESPACE} -o json | grep -oP '"name":"\\K[^"]*' | tr '\n' ','
                    """, returnStdout: true).trim()
                    env.RELEASE_NAME = input message: 'Select the release to destroy:', parameters: [choice(name: 'RELEASE', choices: existingReleases, description: 'Existing releases')]
                    sh """
                        helm uninstall ${env.RELEASE_NAME} \
                        --namespace ${NAMESPACE} \
                        --kubeconfig ${KUBECONFIG}
                    """
                }
            }
        }
    }
    
    post {
        always {
            sh 'rm -f ${KUBECONFIG}'
        }
        success {
            echo "${params.ACTION} of ${params.ACTION == 'Deploy' ? env.SELECTED_CHART : env.RELEASE_NAME} ${params.ACTION == 'Deploy' ? "version ${env.RELEASE_TAG}" : ''} successful!"
        }
        failure {
            echo "${params.ACTION} failed. Please check the logs for more information."
        }
    }
}